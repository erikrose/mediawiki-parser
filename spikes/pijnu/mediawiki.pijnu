wikitext
<toolset>
def setNullValue(node):
	node.value = ''
def parseAllQuotes(node):
	from apostropheParser import parseQuotes
	oldValue = node.value.decode("utf-8")
	newValue = parseQuotes(oldValue)
	node.value = newValue.encode("utf-8")
<definition>
# codes
	LF			: '
'
	CR			: '
'
	EOL			: LF / CR								: drop
	APOSTROPHE		: "\'"									: drop
	L_BRACKET		: "["									: drop
	R_BRACKET		: "\]"									: drop
	L_BRACE			: "{"									: drop
	R_BRACE			: "}"									: drop
	SPACE			: " "									: drop
	PIPE			: "|"									: drop
	EQUAL			: "="									: drop
	BULLET			: "*"									: drop
	HASH			: "#"									: drop
	COLON			: ":"									: drop

	HTTP			: "http://"								: liftValue
	FTP			: "ftp://"								: liftValue
	protocole		: HTTP / FTP								: liftValue

	NOWIKI_BEGIN		: "<nowiki>"								: drop
	NOWIKI_END		: "</nowiki>"								: drop
	BOLD_BEGIN		: "<b>" / "<strong>"							: drop
	BOLD_END		: "</b>" / "</strong>"							: drop
	ITALIC_BEGIN		: "<i>" / "<em>"							: drop
	ITALIC_END		: "</i>" / "</em>"							: drop
	tag			: NOWIKI_BEGIN/NOWIKI_END/BOLD_BEGIN/BOLD_END/ITALIC_BEGIN/ITALIC_END

# character expression
	escChar			: L_BRACKET/R_BRACKET/protocole/PIPE/L_BRACE{2}/R_BRACE{2}/EQUAL/tag
	rawChar			: !escChar [\x20..\xff]
	rawText			: rawChar+								: join parseAllQuotes
	anyChar			: [\x20..\xff]
	anyText			: anyChar+								: join

# text
	pageName		: rawChar+								: join
	templateName		: rawChar+								: join
	address			: !SPACE [\x21..\xff]+							: liftValue
	url			: protocole address							: join
	boldText		: BOLD_BEGIN inline BOLD_END						: liftValue
	italicText		: ITALIC_BEGIN inline ITALIC_END					: liftValue
	value			: EQUAL inline								: join
	optionalValue		: value*								: join
	parameter		: PIPE rawText optionalValue						: liftValue
	ignoredInParameters	: EOL/SPACE								: drop
	parameters		: (parameter/ignoredInParameters)+
	simpleInternalLink	: L_BRACKET L_BRACKET templateName R_BRACKET R_BRACKET			: liftValue
	advancedInternalLink	: L_BRACKET L_BRACKET templateName PIPE inline R_BRACKET R_BRACKET	: liftValue
	internalLink		: simpleInternalLink / advancedInternalLink				: liftValue
	externalLink		: L_BRACKET url SPACE inline R_BRACKET					: liftValue
	link			: internalLink / externalLink
	simpleTemplate		: L_BRACE L_BRACE pageName R_BRACE R_BRACE				: liftValue
	advancedTemplate	: L_BRACE L_BRACE pageName parameters R_BRACE R_BRACE			: liftValue
	template		: simpleTemplate / advancedTemplate
	styledText		: boldText / italicText / link / url / template
	ignoredInNowiki		: (!(NOWIKI_END) [\x20..\xff])+						: join
	nowiki			: NOWIKI_BEGIN ignoredInNowiki+ NOWIKI_END				: liftValue
	inline			: (styledText / rawText / nowiki)+					: @

# line types
	ignoredAfterTitle	: anyText*								: drop
	title6			: EQUAL{6} inline EQUAL{6} ignoredAfterTitle EOL			: liftValue
	title5			: EQUAL{5} inline EQUAL{5} ignoredAfterTitle EOL			: liftValue
	title4			: EQUAL{4} inline EQUAL{4} ignoredAfterTitle EOL			: liftValue
	title3			: EQUAL{3} inline EQUAL{3} ignoredAfterTitle EOL			: liftValue
	title2			: EQUAL{2} inline EQUAL{2} ignoredAfterTitle EOL			: liftValue
	title1			: EQUAL{1} inline EQUAL{1} ignoredAfterTitle EOL			: liftValue

	pre			: SPACE inline								: liftValue
	title			: title6 / title5 / title4 / title3 / title2 / title1

	line			: inline EOL								: liftValue
	blankParagraph		: EOL{2}								: setNullValue
	paragraph		: line+									: liftValue
	paragraphs		: (blankParagraph/EOL/paragraph)+

	bulletListLeaf		: BULLET inline EOL							: liftValue
	bulletSubList		: BULLET bulletListItem							: @
	bulletListItem		: bulletSubList / bulletListLeaf					: @
	bulletList		: bulletListItem+

	numberListLeaf		: HASH inline EOL							: liftValue
	numberSubList		: HASH numberListItem							: @
	numberListItem		: numberSubList / numberListLeaf					: @
	numberList		: numberListItem+

	preformatedLine		: SPACE inline EOL							: liftValue
	preformatedParagraph	: preformatedLine+

	invalidLine		: anyText EOL								: liftValue

	#body			: (bulletList / numberList / preformatedParagraph / title / paragraphs / invalidLine)+
	body			: (bulletList / numberList / preformatedParagraph / title / EOL / paragraphs / invalidLine / EOL)+
